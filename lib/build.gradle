/*
 * Copyright 2014 serso aka se.solovyev
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 * Contact details
 *
 * Email: se.solovyev@gmail.com
 * Site:  http://se.solovyev.org
 */

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion android_sdk_version()
    buildToolsVersion android_build_tools_version()
    defaultConfig {
        minSdkVersion android_min_sdk_version()
        targetSdkVersion android_sdk_version()
        versionCode version_code()
        versionName version_name()
    }

    libraryVariants.all { variant ->
        if (variant.buildType.name.equals("debug")) {
            return
        }

        def variantName = variant.name.capitalize()

        task("android${variantName}Javadoc", type: Javadoc, dependsOn: "assemble$variantName") {
            description "Generates Javadoc for build $variantName."
            destinationDir = new File(destinationDir, variant.baseName)
            source = variant.javaCompile.source
            classpath = files(getBootClasspath(), variant.javaCompile.classpath)
            options.links("http://docs.oracle.com/javase/7/docs/api/");
            options.linksOffline("http://d.android.com/reference/", "${android.sdkDirectory}/docs/reference");
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
        }

        task("android${variantName}JavadocsJar", type: Jar, dependsOn: "android${variantName}Javadoc") {
            description "Bundles Javadoc into jar for build $variantName."
            classifier = 'javadoc'
            destinationDir = new File(destinationDir, variant.baseName)
            from tasks."android${variantName}Javadoc".destinationDir
        }

        artifacts {
            archives tasks."android${variantName}JavadocsJar"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:recyclerview-v7:22.0.0'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task apklib(type: Zip) {
    appendix = extension = 'apklib'

    from 'src/main/AndroidManifest.xml'
    into('res') {
        from android.sourceSets.main.res.srcDirs
    }
    into('src') {
        from android.sourceSets.main.java.srcDirs
    }
    into('src') {
        from android.sourceSets.main.aidl.srcDirs
    }
}


artifacts {
    archives androidSourcesJar
    archives apklib
}

signing {
    sign configurations.archives
}

group = "org.solovyev.android.views"
archivesBaseName = "linear-layout-manager"
version = version_name()

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Linear Layout Manager'
                packaging 'aar'
                description 'Linear Layout Manager which supports WRAP_CONTENT'
                url 'https://github.com/serso/android-linear-layout-manager'

                scm {
                    url 'https://github.com/serso/android-linear-layout-manager'
                    connection 'scm:https://serso@github.com/serso/android-linear-layout-manager.git'
                    developerConnection 'scm:git://github.com/serso/android-linear-layout-manager.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'se.solovyev'
                        name 'Sergey Solovyev'
                        email 'se.solovyev@gmail.com'
                    }
                }
            }
        }
    }
}
